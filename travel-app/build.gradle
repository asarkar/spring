apply plugin: 'spring-boot'
apply plugin: 'idea'
apply plugin: 'war'

apply from: 'https://raw.githubusercontent.com/abhijitsarkar/build-tools/master/gradle-common/build.gradle'

group = 'name.abhijitsarkar.javaee'
version = '0.0.1-SNAPSHOT'

// Rename the property used to choose property files
project.ext.propertiesPluginEnvironmentNameProperty = 'env'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(
                ["org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE"],
                ["net.saliman:gradle-properties-plugin:1.4.4"]
        )
    }
}

apply plugin: 'net.saliman.properties'

repositories {
    mavenCentral()
}

dependencies {
    compile("com.couchbase.client:java-client:2.2.2") {
        exclude group: 'io.reactivex', module: 'rxjava' //by both name and group
    }
    compile("io.reactivex:rxjava:1.1.0")
    compile("com.couchbase.client:core-io:1.2.2")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.projectlombok:lombok:1.16.6")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.4")

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
}

task herokuEnv(type: Copy) {
    from(projectDir) {
        include "gradle-${env}.properties"
        rename("gradle-${env}.properties", '.env')
    }
    into projectDir
}

task stage {
    dependsOn dependsOn: ['build', 'herokuEnv']
}

println "Env: ${env}, Properties: ${project.filterTokens}"

springBoot {
    mainClass = 'name.abhijitsarkar.javaee.travel.TravelApp'
}

bootRun {
    def activeProfiles = System.getProperty('spring.profiles.active') ?: ''

    systemProperties = project.filterTokens + ['spring.profiles.active' : activeProfiles]
}

